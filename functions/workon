# -*- sh -*-
# Description
# ===========
#
# Switch to another environment.
#
# An environment is either a virtualenv-based environment, a Docker
# image or a Docker container. Virtualenv-based environments are
# expected to be in $WORKON_HOME.

emulate -L zsh
setopt nomonitor

local env=$1
local -a venv dimages dcontainers
venv=($WORKON_HOME/*/bin/activate(.N:h:h:ft))
(( $+commands[docker] )) && [[ -w /var/run/docker.sock ]] && {
    dimages=( $(docker image list | awk '(NR > 1 && $1 !~ /^</){printf("%s:%s\n", $1,$2)}') )
    dcontainers=( $(docker container list | awk '(NR > 1){split($NF,names,/,/); for (i in names) printf("%s\n",names[i])}') )
}

# No parameters, list available environment
[[ -n $env ]] || {
    print "INFO: List of available environments:"
    for env in $venv; do
        print " - [virtualenv] $env"
    done
    for image in $dimages; do
        print " - [docker    ] $image"
    done
    for container in $dcontainers; do
        print " - [docker    ] $container"
    done
    return 0
}

[[ $env == "." ]] && env=${PWD:t}

# Compile sudo if needed
local sudo=$ZSH/run/u/$HOST-$UID/sudo
function compile_sudo() {
    if [[ ! -f $sudo ]] || \
           (( $(zstat +mtime $ZSH/misc/sudo.c) > $(zstat +mtime $sudo) )) && \
               (( $+commands[gcc] )) ; then
        gcc -Wall -static $ZSH/misc/sudo.c -o $sudo
    fi
}

# Docker stuff
local setupuser="
for SHELL in ${SHELL}-static $SHELL /bin/bash /bin/sh; do
    [ ! -x \$SHELL ] || break
done
mkdir -p /tmp
chmod 1777 /tmp
if ! id $USER > /dev/null 2> /dev/null; then
    echo $(getent passwd $(id -u)) >> /etc/passwd
    echo $(getent group $(id -g)) >> /etc/group
    # Put sudo configuration even when no sudo (in case it gets installed later)
    mkdir -p /etc/sudoers.d
    echo \"$USER ALL=(ALL) NOPASSWD: ALL\" > /etc/sudoers.d/$USER
    chmod 0440 /etc/sudoers.d/$USER
    [ -x /usr/bin/sudo ] || [ ! -x $sudo ] ||  {
        mkdir -p /usr/bin
        cp $sudo /usr/bin/_sudo
        chown 0:$(id -gn) /usr/bin/_sudo
        chmod 4750 /usr/bin/_sudo
        cat <<'EOF' > /usr/bin/sudo
#!/bin/sh
export PATH=/usr/local/bin:/usr/local/sbin:/usr/bin:/usr/sbin:/bin:/sbin

exec /usr/bin/_sudo \"\$@\"
EOF
        chmod +x /usr/bin/sudo
    }
fi
[ ! -f /etc/nsswitch.conf ] || sed -i 's/ sss//' /etc/nsswitch.conf
"

# Docker images
[[ ${dimages[(r)$env]} == $env ]] && {
    local image=${env}
    local tmp=$(mktemp -d)
    compile_sudo
    <<EOF > $tmp/start
#!/bin/sh
$setupuser
exec env HOME=$HOME TERM=$TERM DOCKER_CHROOT_NAME=$env SHLVL= \
     $sudo -u $(id -u) -g $(id -g) -c '$PWD' \$SHELL -i -l
EOF
    (( $+commands[tic] )) && (( $+commands[infocmp] )) && infocmp $TERM | tic -
    docker container run -t -i \
           $([[ -d /usr/share/zsh ]] && print -- -v /usr/share/zsh:/usr/share/zsh-static:ro) \
           $([[ -x ${SHELL}-static ]] && print -- -v ${SHELL}-static:${SHELL}-static:ro) \
           -v $HOME:$HOME \
           -v $tmp:$tmp \
           -w $PWD \
           -u 0 \
           --rm \
           -h ${${${image##*/}:gs/:/-}:gs/./-} \
           --entrypoint /bin/sh \
           $image $tmp/start
    local ret=$?
    rm -f $tmp/start && rmdir $tmp
    return $ret
}

# Docker containers
[[ ${dcontainers[(r)$env]} == $env ]] && {
    local id=$(docker container inspect --format '{{.State.Pid}}' $env)

    # We need to mount $HOME inside the container, that's quite
    # hacky: we get the device we need to mount, we mount it
    # somewhere, then bind mount the home directory in the right
    # place. All this with elevated privileges. We also create our
    # user, with sudo rights. Most inspiration comes from here:
    #  http://blog.dehacked.net/lxc-getting-mounts-into-a-running-container/
    #
    # Also, from Docker 0.9, see:
    #  http://jpetazzo.github.io/2014/03/23/lxc-attach-nsinit-nsenter-docker-0-9/
    #  http://www.sebastien-han.fr/blog/2014/01/27/access-a-container-without-ssh/
    #
    # From Docker 1.3, see `docker exec'.
    #
    # So, this needs nsenter which needs a recent util-linux.
    #
    # $HOME is mounted into a dedicated mount namespace. However the
    # container is still modified for user and sudo.
    local homemnt=${${(f)"$(df --output=target $HOME)"}[-1]}
    local homedev=$(readlink -f ${${(f)"$(df --output=source $HOME)"}[-1]})
    local enter=$(mktemp --tmpdir nsenter-XXXXXXXXX)
    local environ=$(docker container inspect --format "{{range .Config.Env}}export {{.}}"$'\n'"{{end}}" $env)
    compile_sudo
    ${=${(%):-%(!..sudo -v)}}
    coproc ${=${(%):-%(!..sudo -n)}} =nsenter -m -t $id -- unshare --mount /bin/sh -ec "
#!/bin/sh
tmp=\$(mktemp -d)
mkdir -p ${HOME}
[ -b /dev/home-directory ] || \
    mknod /dev/home-directory b $(($(stat -c 0x%t ${homedev}))) $(($(stat -c 0x%T ${homedev})))
mount -n /dev/home-directory \$tmp
rm /dev/home-directory
mount -n --bind \$tmp/${HOME#$homemnt} $HOME
umount -n \$tmp 2> /dev/null
rmdir \$tmp

$setupuser

# Setup a command to enter this environment
>  $enter echo \"$environ\"
>> $enter echo exec env HOME=$HOME TERM=$TERM DOCKER_CHROOT_NAME=$env SHLVL= \
   $sudo -u $(id -u) -g $(id -g) -c \$PWD \$SHELL -i -l

echo \$\$
read nothing
rm $enter
"
    read -p pid
    if [[ -n $pid ]]; then
        (( $+commands[tic] )) && (( $+commands[infocmp] )) && infocmp $TERM | tic -
        ${(%):-%(!..sudo)} =nsenter --mount=/proc/$pid/ns/mnt -u -i -n -p -t $id -- /bin/sh $enter
        ret=$?
        print -p done
        return $ret
    fi
    return 1
}

# Virtualenv
[[ ${venv[(r)$env]} == $env ]] && {(
    local activate="$WORKON_HOME/$env/bin/activate"
    local VIRTUAL_ENV_DISABLE_PROMPT=1
    local NODE_VIRTUAL_ENV_DISABLE_PROMPT=1
    source $activate # nowadays, it really just push virtualenv's bin in PATH

    append_path() {
        mkdir -p $1
        path=( $1 $path )
    }

    # Gems.
    # GEM_HOME is where gems will be installed.
    # GEM_PATH is where gems are searched
    export GEM_HOME=$VIRTUAL_ENV/gems
    export GEM_PATH=$GEM_HOME
    append_path $GEM_HOME/bin

    # Go
    export GOPATH=$VIRTUAL_ENV/go
    append_path $GOPATH/bin

    # C (install with ./configure --prefix=$VIRTUAL_ENV)
    export LD_LIBRARY_PATH=$VIRTUAL_ENV/lib${LD_LIBRARY_PATH:+:$LD_LIBRARY_PATH}
    export PKG_CONFIG_PATH=$VIRTUAL_ENV/lib/pkgconfig
    append_path $VIRTUAL_ENV/sbin

    # OCaml (through OPAM)
    (( $+commands[opam] )) && {
        export OPAMROOT=$VIRTUAL_ENV/opam
        [[ -d $OPAMROOT ]] && \
            eval $(opam config env)
    }

    # node.js workaround
    [[ -z $NPM_CONFIG_PREFIX ]] || export npm_config_prefix=$NPM_CONFIG_PREFIX

    export PATH
    exec $SHELL -i
); return }

print "ERROR: environment $env does not exist"
return 2
