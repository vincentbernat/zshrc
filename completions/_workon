#compdef workon

_environments() {
    local -a virtualenvs images containers
    local expl

    virtualenvs=( ${WORKON_HOME}/*/bin/activate(.N:h:h:t) )
    _wanted environments expl 'virtualenv environment' compadd \
        -a virtualenvs

    (( $+commands[docker] )) && [[ -w /var/run/docker.sock ]] && {
        images=(${(f)"$(docker image list 2> /dev/null | awk '(NR > 1 && $1 !~ /^</){printf("%s:%s\n", $1,$2)}')"})
        _wanted images expl 'docker images' compadd \
            -a images

        containers=(${(f)"$(docker container list 2> /dev/null | awk '(NR > 1) { split($NF,names,/,/); for (i in names) printf("%s:%7s\n",names[i],$2)}')"})
        _describe -t containers-active "running containers" containers
    }
}

_arguments \
    '1:::_environments' && return 0

# Local Variables:
# mode: Shell-Script
# sh-indentation: 4
# indent-tabs-mode: nil
# sh-basic-offset: 4
# End:
