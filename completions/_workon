#compdef workon

_environments() {
    local -a virtualenvs images containers docker
    local expl

    (( $+commands[docker] )) \
        && containers=(${(f)"$(docker container list --format '{{ .Names }}' | tr ',' '\n' 2> /dev/null)"}) \
        && (( $#containers )) \
        && _describe -t containers-active "running Docker containers" containers
    (( $+commands[podman] )) \
        && containers=(${(f)"$(podman container list --format '{{ .Names }}' | tr ',' '\n' 2> /dev/null)"}) \
        && (( $#containers )) \
        && _describe -t containers-active "running Podman containers" containers

    virtualenvs=( ${WORKON_HOME}/*/bin/activate(.N:h:h:t) )
    _wanted environments expl 'virtualenv environment' compadd \
        -a virtualenvs

    (( $+commands[docker] )) \
        && images=(${(f)"$(docker image list --format '{{ .Repository }}:{{ .Tag }}' 2> /dev/null)"}) \
        && (( $#images )) \
        && _wanted images expl 'docker images' compadd -a images
    (( $+commands[podman] )) \
        && images=(${(f)"$(podman image list --format '{{ .Repository }}:{{ .Tag }}' 2> /dev/null)"}) \
        && (( $#images )) \
        && _wanted images expl 'podman images' compadd -a images
}

_arguments \
    '1:::_environments' && return 0

# Local Variables:
# mode: Shell-Script
# sh-indentation: 4
# indent-tabs-mode: nil
# sh-basic-offset: 4
# End:
